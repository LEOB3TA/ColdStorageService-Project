System/* -trace*/prototipo2

// Messaggi scambiati con mocktruck 
Request deposit : deposit(_)
Request sendTicket : sendTicket(TICKETID)
Request storeFood : storeFood(FW)
Reply storeAccepted : storeAccepted(TICKETID) for storeFood
Reply storeRejected : storeRejected(_) for storeFood
Reply chargeTaken : chargeTaken(_) for deposit
Reply ticketValid : ticketValid(_) for sendTicket
Reply ticketNotValid : ticketNotValid(_) for sendTicket
Reply ticketExpired : ticketExpired(_) for sendTicket

// Messaggi scambiati con transporttrolley
Request pickup:    pickup(_) 
Reply  pickupdone:  pickupdone(_) for pickup

// Messaggi scambiati col basicrobot23


Request engage        : engage(ARG)
Reply   engagedone    : engagedone(ARG) for engage
Reply   engagerefused : engagerefused(ARG) for engage
Dispatch disengage    : disengage(ARG)

Request  doplan     : doplan( PATH, OWNER, STEPTIME )
Reply doplandone    : doplandone( ARG ) for doplan
Reply doplanfailed  : doplanfailed( ARG ) for doplan

Request moverobot    :  moverobot(TARGETX, TARGETY)
Reply moverobotdone  :  moverobotok(ARG) for moverobot
Reply moverobotfailed:  moverobotfailed(PLANDONE, PLANTODO) for moverobot

Dispatch setrobotstate: setpos(X,Y,D)
Dispatch setdirection : dir( D )  	  //D =up|down!left|right
Dispatch cmd       	: cmd(MOVE)		 //MOVE=w|s|d|a|r|l|h

Event local_movef : movef(_)

//Sonar e movimenti robot
Event sonardata : distance(D) //dataclenaer
//Event obstacle : obstacle(D)	//distancefilter
Event alarm		: alarm(X)		//per tt TODO: l'argomento � utile?
Event stop : stop(_)
Event resume:resume(_)
Dispatch sonaractivate: info(D) //D = DLIMIT

//Led 
Dispatch ledCmd : ledCmd ( CMD )

Dispatch coapUpdate: coapUpdate(RES,VAL)

//Messaggi autoinviati transporttrolley
Dispatch gotomovetoport : gotomovetoport(_)
Dispatch gotodepositactionended : gotodepositactionended(_)
Dispatch gotorobottohome : gotorobottohome(_)

Context ctxbasicrobot ip [host="127.0.0.1" port=8020]
Context ctxprototipo2 ip [host="localhost" port=8099]
//Context ctxtruck ip[host="127.0.0.1" port=8092] //indirizzo interno alla rete

// Attori Esterni
ExternalQActor basicrobot context ctxbasicrobot
//ExternalQActor mocktruck context ctxtruck

//Sonar

//CodedQActor sonar  context ctxrasp className "rx.sonarHCSR04Support23"   //SU RASP
CodedQActor sonar context ctxprototipo2 className "rx.sonarSimulator" //in LOCALE
CodedQActor datacleaner    context ctxprototipo2 className "rx.dataCleaner"
//CodedQActor distancefilter context ctxprototipo2 className "rx.distanceFilter"


QActor mocktruck context ctxprototipo2{
    [#
       	var FW = 0
        var DT = 1000L  //DT= driver time tempo che ci mette il driver dopo aver ricevuto la richiesta per arrivare alla INDOOR
        var TICKETID = 0
        val truckstate = state.TruckState()

        fun initDriver(){
        	FW =  kotlin.random.Random.nextInt(1, 101)
        	DT = kotlin.random.Random.nextLong(1, 5001)
        }
    #]
    State s0 initial{
		println("$name |	started") color green
		println("this is an infinite loop, you need to kill the program to stop this") color red
		discardMsg Off

    }Goto idle

    State idle {
        println("$name |	in idle") color green
        [#
        	initDriver()
        	delay(kotlin.random.Random.nextLong(1, 15001))
        #]
    }Goto sendStore

    State sendStore {
    	 println("$name |	sendStore") color green
        request coldstorageservice -m storeFood : storeFood($FW)

    } Transition t0 whenTime 60000 -> handleError //se non arriva una risposta entro 60 secondi c'è un errore interno al sistema, il driver abbandona
    whenReply storeAccepted -> accepted
                    whenReply storeRejected -> rejected

   State rejected{
   		println("$name |	request rejected") color green
   }Goto idle

    State accepted{
   		onMsg (storeAccepted : storeAccepted(TICKETID)){
   		[#
   			TICKETID = payloadArg(0).toInt()
   		#]
   		}

   		println("$name |	request accepted") color green
   }Transition t0 whenTimeVar DT -> sendTicket

   State sendTicket{
   		 request coldstorageservice -m sendTicket : sendTicket($TICKETID)
   		 println("$name |	send ticket") color green
   } Transition t0 whenTime 60000 -> handleError //se non arriva una risposta entro 60 secondi c'� un errore interno al sistema, il driver abbandona
   					whenReply ticketValid -> sendDeposit
    				whenReply ticketExpired -> handleTicketExpired


    State sendDeposit{
        request coldstorageservice -m deposit : deposit(_)
        println("$name |	send deposit") color green
    } Transition t0  whenTime 60000 -> handleError //se non arriva una risposta entro 60 secondi c'� un errore interno al sistema, il driver abbandona
    					whenReply chargeTaken -> idle

    State handleError{
    	println("$name |	COLD STORAGE SERVICE ERROR") color red
    } Goto idle

	State handleTicketExpired{
		println("$name |	ticket expired") color green
	} Goto idle

	State handleTicketNotValid{
		println("$name |	ticket Not valid retry") color green
	} Goto sendTicket

}

QActor coldstorageservice context ctxprototipo2 {

 	[#
        	val MAXW = resources.ColdStorageService.getMAXW()
			val TICKETTIME = resources.ColdStorageService.getTICKETTIME()
		   	var currentWeightStorage = resources.ColdStorageService.getCurrentWeightStorage()
		   	var requestWeightToStore = 0.0
		   	var TICKETNUMBER = resources.ColdStorageService.getTicketNumber()
		   	#]

    State setup initial {
        	println("$name |	setup") color blue
        	discardMsg Off

    }

  Goto idle

    State idle {
        println("$name |	in idle") color green
    }
    Transition t0 whenRequest storeFood -> requestEvaluation
    			  whenRequest sendTicket -> ticketEvaluation
    			  whenRequest deposit -> charged
    			  whenReply pickupdone -> taken
    			  whenEvent local_movef -> error

    /*
     *  REQUEST EVALUATION
     *  1. Funzione canStore gestisce la logica
     * 	2a. Se esito positivo, va in stato acceptRequest
     * 	2b. Altrimenti va in stato rejectRequest
     */
    State requestEvaluation {
    	        onMsg(storeFood : storeFood(FW)){
           println("Request evaluation to store ${payloadArg(0)} kg") color blue
           [#requestWeightToStore=payloadArg(0).toDouble()#]

       }
    } Goto acceptRequest if[# resources.ColdStorageService.canStore(requestWeightToStore) #]
                            else rejectRequest

     /*
      *  ACCEPT REQUEST
      *  Generazione TICKET +
      */
     State acceptRequest{
    	[#
    		TICKETNUMBER = resources.ColdStorageService.getTicketNumber()
    		var TICKET : resources.model.Ticket = resources.model.Ticket(TICKETNUMBER, TICKETTIME)
    		resources.ColdStorageService.incrementTicketNumber()
    		resources.ColdStorageService.getTicketList().add(TICKET)
    	#]
        replyTo storeFood with storeAccepted : storeAccepted($TICKETNUMBER) 
    }Goto idle

	/*
	 *  REJECT REQUEST
	 */
    State rejectRequest{
        replyTo storeFood with storeRejected : storeRejected(_)
    }Goto idle

    /* Non usato
     State dropout{
    	forward transporttrolley -m dropout : dropout(FW)
    	forward transporttrolley -m gotohome : gotohome(_)
	}Goto idle
*/

    State charged{
    	println("$name |	in charged") color green
    	request transporttrolley -m pickup : pickup(_)
    }Goto idle

    State taken{
    	println("$name |	in taken") color green
    	replyTo deposit with chargeTaken : chargeTaken(_)
    }Goto idle



    /*
	 *  TICKET EVALUATION
	 *  Il coldStorageService controlla se il ticket ricevuto in input � uguale a quello che si aspetta.
	 * 	In caso di errore, il truck pu� riprovare ad inserire il numero del ticket fin quando non scade il TICKETTIME.
	 *
	 */
    State ticketEvaluation {
		 onMsg(sendTicket : sendTicket(TICKETID)){
		 	println("Ticket evaluation of ticket id ${payloadArg(0)}") color blue
    	        	[#
    	        		 val TICKETID = payloadArg(0).toInt()
    	         	 val TICKETEVALUATION = resources.ColdStorageService.evaluateTicket(TICKETID) #]
    	        	if [#TICKETEVALUATION == resources.TicketEvaluationResponse.VALID #] {
    	        		[#
    	        			val TICKET = resources.ColdStorageService.getTicketById(TICKETID)
							resources.ColdStorageService.getTicketList().remove(TICKET)
    	        		#]
    	        		replyTo sendTicket with ticketValid : ticketValid(_)
    	        	}
					if [#TICKETEVALUATION == resources.TicketEvaluationResponse.EXPIRED #] {
    	        		[#
    	        			 resources.ColdStorageService.incrementRejectedRequestCounter()
    	        			val TICKET = resources.ColdStorageService.getTicketById(TICKETID)
							resources.ColdStorageService.getTicketList().remove(TICKET)
    	        		#]
    	        		   println("Ticket of id ${payloadArg(0)} is expired - Reject Request") color blue
    						replyTo sendTicket with ticketExpired : ticketExpired(_)
    	        	}
					
					if [#TICKETEVALUATION == resources.TicketEvaluationResponse.INVALID #] {
    	        		[#
    	        			val TICKET = resources.ColdStorageService.getTicketById(TICKETID)
							resources.ColdStorageService.getTicketList().remove(TICKET)
    	        		#]
    	        			println("Inserted ticket id is not valid") color red
    					replyTo sendTicket with ticketNotValid : ticketNotValid(_)
    	        	}
					
       				}
    }Goto idle
    
   State error{
   	println("$name | robot failed to move") color red
 		println("$name | close") color red
 		[# System.exit(0) #]
   }
}

QActor transporttrolley context ctxprototipo2{ //TODO  controllare cosa succede se arriva una stop e quindi avviene moverobotfailed per alarm
	[#
		var CRX = 4
		var CRY = 3
		var INDOORX= 0
		var INDOORY= 4
		var HOMEX= 0
		var HOMEY= 0
		//println("$HOMEX,$HOMEY")
		var TICKETID = 0
		val tTState = state.TransportTrolleyState()
		val MyName = name
		val ts = kotlin.time.TimeSource.Monotonic
		var m1 = ts.markNow()
		val MINT : kotlin.time.Duration= 1.seconds
		var savedState = tTState.getCurrState()
	#]
	State init initial{
		discardMsg Off
		// engage
		println("$name | request engage") color green
		request basicrobot -m engage : engage ($MyName, 305)
		//printCurrentMessage color black
	}Transition t0 //whenTime 10000 -> error
				   whenReply engagedone -> idle
				   whenReply engagerefused -> waitforfree

   State error{ //DEBUG
   		println("$name | basic robot error") color red
   		println("$name | disengaging....") color red
   		forward basicrobot -m disengage : disengage( $MyName )
   		println("$name | disengaged....") color red
   }

	State waitforfree{
		println("$name | already engaged") color green
	} Transition t0 whenTime 10000 -> idle

	State idle {
		println("$name | engaged") color green
		[#
			tTState.setCurrState(state.CurrStateTrolley.IDLE)
			tTState.setCurrPosition(state.TTPosition.HOME)
		#]
		updateResource[#tTState.toJsonString()#]
		// State print for debug
		//[#
		//	println(tTState.getCurrState())
		//	println(tTState.getCurrPosition())
		//#]
		forward basicrobot -m setrobotstate : setpos (0,0,d) // d= down
		println("$name | waiting for commands.") color green
  	}
 	Transition t0 whenRequest pickup -> moverobottoindoor

 	State moverobottoindoor {
 		//move robot to indoor
 		onMsg(pickup :pickup(_)){
 			[#
			tTState.setCurrState(state.CurrStateTrolley.PICKINGUP)
			tTState.setCurrPosition(state.TTPosition.INDOOR)
			#]
 		}
		updateResource[#tTState.toJsonString()#]
 		println("$name | moving robot to indoor.") color green
 		request basicrobot -m moverobot:moverobot($INDOORX,$INDOORY)
		
		
		
 	} 
 	Transition t0 whenEvent stop -> handlerobotstopped
 				  whenReply moverobotdone -> movetoport
 				// whenReply moverobotfailed -> robotmovefailed 
 				  
  	State handlerobotstopped{  //TODO controllare se else va bene o è scrivibile in un modo più easy
  		println("$name |handle robot stopped") color green
  		[#
  			if ((m1+MINT).hasPassedNow()){	
  				m1 = ts.markNow()
  				savedState = tTState.getCurrState()
  				tTState.setCurrState(state.CurrStateTrolley.STOPPED)
  		#]
  			emit alarm : alarm(_) //to stop basic robot
			updateResource[#tTState.toJsonString()#]
  		[#	
  			 }/*else{ 
  		#]
  				println("ignored stop signal") color red
  				[#tTState.setCurrState(savedState)#]
				updateResource[#tTState.toJsonString()#]
				
				[# 	when {
							tTState.getCurrState() == state.CurrStateTrolley.PICKINGUP -> #] request basicrobot -m moverobot:moverobot($INDOORX,$INDOORY)[#
							tTState.getCurrState() == state.CurrStateTrolley.MOVINGTOPORT -> #] request basicrobot -m moverobot:moverobot($CRX,$CRY)[#
							tTState.getCurrState() == state.CurrStateTrolley.MOVINGTOHOME ->{ #] request basicrobot -m moverobot:moverobot($HOMEX,$HOMEY)
							[#}}#]
  		[#	}*/#]
  		
		
  			
  	}Transition t0  whenReply moverobotdone -> goahead
  					whenEvent stop -> handlerobotstopped
  					whenEvent resume -> resumerobot
  	  				//whenReply moverobotfailed -> robotmovefailed 
  					
	State resumerobot{
		println("$name | resume robot") color green
		  		onMsg(resume: resume(_)){
		  		
  				[#tTState.setCurrState(savedState)#]
  				updateResource[#tTState.toJsonString()#]
  						[# 	when {
							tTState.getCurrState() == state.CurrStateTrolley.PICKINGUP -> #] request basicrobot -m moverobot:moverobot($INDOORX,$INDOORY)[#
							tTState.getCurrState() == state.CurrStateTrolley.MOVINGTOPORT -> #] request basicrobot -m moverobot:moverobot($CRX,$CRY)[#
							tTState.getCurrState() == state.CurrStateTrolley.MOVINGTOHOME ->{ #] request basicrobot -m moverobot:moverobot($HOMEX,$HOMEY)
						
							[#}}#]
				}
	}Transition t0  whenEvent stop -> handlerobotstopped
					whenReply moverobotdone -> goahead
					//whenReply moverobotfailed -> robotmovefailed
  					
	State goahead{
		println("$name | go ahead with next state") color green
		[# 	when {
							tTState.getCurrState() == state.CurrStateTrolley.PICKINGUP -> #] forward transporttrolley -m gotomovetoport : gotomovetoport(_)[#
							tTState.getCurrState() == state.CurrStateTrolley.MOVINGTOPORT -> #] forward transporttrolley -m gotodepositactionended : gotodepositactionended(_)[#
							tTState.getCurrState() == state.CurrStateTrolley.MOVINGTOHOME ->{ #] 
							forward transporttrolley -m gotorobottohome : gotorobottohome(_)
								
		[#
			}}#]
	}Transition t0 whenMsg gotomovetoport -> movetoport
					whenMsg gotodepositactionended -> depositactionended
					whenMsg gotorobottohome -> corrDir
  					
  	


	//when robot has taken kg from camion -> move robot to coldroom
	State movetoport{
		[#
			tTState.setCurrState(state.CurrStateTrolley.MOVINGTOPORT)
			tTState.setCurrPosition(state.TTPosition.ONTHEROAD)
		#]
		updateResource[#tTState.toJsonString()#]
		//send event "robot is in indoor"
		println("$name | robot is in indoor") color green
		println("$name | moving robot to coldroom") color green
		// da usare col css
		replyTo pickup with pickupdone:pickupdone(_)
		request basicrobot -m moverobot:moverobot($CRX,$CRY)
		
		
	}
	Transition t0  whenEvent stop -> handlerobotstopped
 				  whenReply moverobotdone -> depositactionended
 				// whenReply moverobotfailed -> robotmovefailed //TODO controllare cosa accade in caso di emissione dell'evento alarm perchè non è da escludere che non funzioni

	State depositactionended{
		[#
			tTState.setCurrState(state.CurrStateTrolley.DROPPINGOUT)
			tTState.setCurrPosition(state.TTPosition.PORT)
		#]
		updateResource[#tTState.toJsonString()#]

		println("$name | robot is in coldroom") color green
		//emitlocal local_dropoutdone: local_dropoutdone
		println("$name | depositaction ended") color green
		println("$name | waiting for next move" ) color green
		[#
			tTState.setCurrState(state.CurrStateTrolley.IDLE)
			tTState.setCurrPosition(state.TTPosition.PORT)
		#]
		updateResource[#tTState.toJsonString()#]
	}
	Transition t0 whenTime 3000 -> robottohome
	whenRequest pickup -> moverobottoindoor
	//se arriva un messagio prima di 3s moverobottoindoor altrimenti torno in home opppure se c'� gi� una pickup in coda

	State robottohome{
		[#
			tTState.setCurrState(state.CurrStateTrolley.MOVINGTOHOME)
			tTState.setCurrPosition(state.TTPosition.ONTHEROAD)
		#]
		updateResource[#tTState.toJsonString()#]
			println("$name | back to home" ) color green
		//send event "robot is in home"
		request basicrobot -m moverobot:moverobot($HOMEX,$HOMEY)
		/*delay 6300
		forward basicrobot -m cmd:cmd(l)*/
	}
	Transition t0  whenReply moverobotdone -> corrDir
					whenEvent stop -> handlerobotstopped
	  			  //whenReply moverobotdone -> idle
 				 // whenReply moverobotfailed -> robotmovefailed


	State corrDir{
		forward basicrobot -m cmd:cmd(l)
	}Goto idle

 	//control failed robot moves
 	State robotmovefailed{
 		println("$name | robot failed to move") color red
 		emit local_movef : local_movef(_)
 		println("$name | close") color red
 		[# System.exit(0) #]
 	}

}


// Implementazione sonar 
//contesto: nello stesso contensto fino a quando non si mette nel rasp

QActor sonar23 context ctxprototipo2{ //TODO mandare eventi condizionati dallo stato
	[#var DLIMIT = 30 
		var D = 0
		var handled=false
	var Appl = sysUtil.getActor("transporttrolley") != null #]
	State s0 initial{
		println("sonar | start with appl: $Appl")
	}Goto work
	
	State work{
		updateResource[# "Sonar waiting"#]
	}Transition t0 whenEvent sonardata -> handlesonardata

	State handlesonardata{
		updateResource [# "sonar23 handles $currentMsg" #]
		onMsg(sonardata : distance(D)){
			[#D = payloadArg(0).toInt()#]
			if [#D<DLIMIT && handled==false#]{
				[#handled=true#]
				println("$name handleobstacle STOP ${payloadArg(0)}") color magenta
				emit stop : stop(_)	
			}
			if[#D>=DLIMIT && handled==true#]{
				[#handled=false#]
				println("$name sonardata RESUME ${payloadArg(0)}") color magenta
				emit resume: resume(_)
			}
		}
	}Goto work	              
}



QActor ledqakactor context ctxprototipo2 {
	[#
		val ledState = state.LedState()
		ledState.setState(state.LState.OFF)
		var current = ledState.getCurrState()
	#]

  State s0 initial{
		println("${name} STARTS")
	}
	Transition t0 whenMsg ledCmd -> doCmd

	State doCmd{ //da codificare i comandi del raspberry sprint4 
		onMsg( ledCmd : ledCmd(CMD) ){
			[# var Cmd = payloadArg(0) #]
			if [# Cmd=="ON" #]{
				//[# CommUtils.outyellow( "${name} - on") #]
				[# 
					ledState.setState(state.LState.ON)
					current = ledState.getCurrState()
				#]
				updateResource[#ledState.toJsonString()#]
				println("${name} - $current") color magenta
			} if [# Cmd=="OFF" #]{
				//[# CommUtils.outyellow( "${name} - off")#]
				[# 
					ledState.setState(state.LState.OFF)
					current = ledState.getCurrState()	
				#]
				updateResource[#ledState.toJsonString()#]
				println("${name} - $current") color magenta
			}if [# Cmd=="BLINK" #] {
				//[# CommUtils.outyellow( "${name} - off")#]
				[# 
					ledState.setState(state.LState.BLINKS)
					current = ledState.getCurrState()
				#]
				updateResource[#ledState.toJsonString()#]
				println("${name} - $current") color magenta
			}
		}
	}
	Transition t0 whenMsg ledCmd -> doCmd
}

QActor stateobservercontroller context ctxprototipo2{ //ctxpc{
[# 
	var state = ""	
	var pos= ""
#] 
	State s0 initial{
		observeResource transporttrolley
	}
	Transition t0
	whenMsg coapUpdate -> upds
	
	State upds{
		[#
			state = "${currentMsg.toString().substringAfter("currState\":\"").substringBefore("\"")}"
			pos = "${currentMsg.toString().substringAfter("currPosition\":\"").substringBefore("\"")}"
		#]
		[#
			when{
				pos == "HOME" -> #] forward ledqakactor -m ledCmd : ledCmd(OFF)
				[#state == "PICKINGUP" || state == "MOVINGTOPORT" || state == "MOVINGTOHOME" ->#] forward ledqakactor -m ledCmd : ledCmd(BLINK)
				[#state == "STOPPED" ->#] forward ledqakactor -m ledCmd : ledCmd(ON)
			[#}#]
	}Transition t0 
	whenMsg coapUpdate -> upds

}



